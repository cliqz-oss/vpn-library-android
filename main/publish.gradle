apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group = 'com.cliqz'
version = vpn_library_version

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/main-normal-release.aar")
            groupId
            artifactId 'com.cliqz.vpnlib'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: ""
    key = System.getenv('BINTRAY_KEY') ?: ""
    publications = [ 'Production' ]
    pkg {
        name = 'vpn-library-android'
        desc = 'VPN Library for Android'
        repo = 'cliqz-oss'
        userOrg = 'cliqz'
        licenses = ['GPL-3']
        websiteUrl = 'https://github.com/cliqz-oss/vpn-library-android'
        issueTrackerUrl = 'https://github.com/cliqz-oss/vpn-library-android/issues'
        vcsUrl = 'https://github.com/cliqz-oss/vpn-library-android.git'
        publicDownloadNumbers = true

        version {
            name = vpn_library_version
            vcsTag = vpn_library_version
            released = new Date()
        }
    }
}
